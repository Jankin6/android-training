import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'org.greenrobot.greendao'


Properties prop = new Properties();
prop.load(new FileInputStream(file("../signing.properties")))

// 需要生成新表时打开
//greendao {
//    schemaVersion 4
//    daoPackage 'com.goxueche.app.greendao.gen'
//    targetGenDir 'src/main/java'
//}

android {
    signingConfigs {
        release {
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
            storeFile file(prop['KEYSTORE_FILEPATH'])
            storePassword prop['STORE_PASSWORD']
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.wangjj.android_training"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                def fileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${variant.productFlavors[0].name}_${buildTime()}.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }

    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    productFlavors {
        xiaomi {}
        huawei {}
        baidu {}
        wandoujia {}
    }
    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.2.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.jakewharton:butterknife:8.1.0'
    apt 'com.jakewharton:butterknife-compiler:8.1.0'
    compile 'com.google.dagger:dagger:2.5'
    apt 'com.google.dagger:dagger-compiler:2.5'
    provided 'javax.annotation:jsr250-api:1.0'
    compile 'com.android.volley:volley:1.0.0'
    compile 'com.orhanobut:logger:1.15'
    // 友盟统计
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.android.support:design:25.2.0'
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.7.0'
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.trello:rxlifecycle:1.0'
    compile 'com.trello:rxlifecycle-components:1.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'
    compile 'org.greenrobot:greendao:3.2.0'


}

def buildTime() {
    def df = new SimpleDateFormat("MM-dd")
    df.setTimeZone(TimeZone.getDefault())
    return df.format(new Date())
}